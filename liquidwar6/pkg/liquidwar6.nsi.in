; liquidwar.nsi
;
; This script is based on example2.nsi

;--------------------------------

; The name of the installer
Name "@PACKAGE_STRING@"

; The file to write
OutFile "@PACKAGE_TARNAME@-@PACKAGE_VERSION@-ms-windows-installer.exe"

; The default installation directory
InstallDir "$PROGRAMFILES\@PACKAGE_NAME@"

; Registry key to check for directory (so if you install again, it will 
; overwrite the old one automatically)
InstallDirRegKey HKLM "Software\@LW6_HKEY@" "Install_Dir"

;--------------------------------

; Pages

Page components
Page directory
Page instfiles

UninstPage uninstConfirm
UninstPage instfiles

;--------------------------------

; The stuff to install
Section "@PACKAGE_NAME@ (required)"

  SectionIn RO
  
  ; Set output path to the installation directory.
  SetOutPath $INSTDIR

  ; Put file there
  File "liquidwar6.exe"
  File "*.txt"
  File "*.dll"
  File /r "script"
  File /r "data"
  File /r "map"
  File /r "music"
  File /r "doc"
  File /r "guile"
  
  ; Write the installation path into the registry
  WriteRegStr HKLM "SOFTWARE\@LW6_HKEY@" "Install_Dir" "$INSTDIR"
  
  ; Write the uninstall keys for Windows
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@LW6_HKEY@" "DisplayName" "@PACKAGE_STRING@"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@LW6_HKEY@" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@LW6_HKEY@" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@LW6_HKEY@" "NoRepair" 1
  WriteUninstaller "uninstall.exe"
  
SectionEnd

; Optional section (can be disabled by the user)
Section "Start Menu Shortcuts"

  CreateDirectory "$SMPROGRAMS\@PACKAGE_NAME@"
  CreateShortCut "$SMPROGRAMS\@PACKAGE_NAME@\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\@PACKAGE_NAME@\@PACKAGE_NAME@.lnk" "$INSTDIR\liquidwar6.exe" "" "$INSTDIR\lwwin.exe" 0
  CreateShortCut "$SMPROGRAMS\@PACKAGE_NAME@\Documentation.lnk" "$INSTDIR\doc\html\index.html" "" "$INSTDIR\doc\html\index.html" 0
  
SectionEnd

;--------------------------------

; Uninstaller

Section "Uninstall"
  
  ; Remove registry keys
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\@LW6_HKEY@"
  DeleteRegKey HKLM "SOFTWARE\@LW6_HKEY@"

  ; Remove shortcuts, if any
  RMDir /r "$SMPROGRAMS\@PACKAGE_NAME@"

  ; Remove directories used
  RMDir /r "$INSTDIR"

SectionEnd
